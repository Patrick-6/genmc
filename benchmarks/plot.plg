set autoscale
#set term pngcairo font 'Inconsolata,11'
set terminal pdf font 'Libertine,9'

#set key autotitle columnheader
set xlabel 'Benchmarks sorted according to runtime for each tool'
set ylabel 'Execution Time in seconds (logscale)'
#set title 'Scaling of different tools'
set output './images/scaling.pdf'

# do a little parsing on the input names
toolnum = words(filenames)
array titles[toolnum]
array files[toolnum]

i = 0
do for [file in filenames] {
   i = i + 1
   dot1 = strstrt(file, '.')
   tool = file[0:dot1-1]
   if (tool eq 'cdschecker') {
      titles[i] = 'cds-checker'
   }
   if (tool eq 'nidhugg') {
      modelext = file[dot1+1:]
      dot2 = strstrt(modelext, '.')
      model = modelext[0:dot2-1]
      titles[i] = 'Nidhugg'.'-'.model
   }
   if (tool eq 'rcmc') {
      modelext = file[dot1+1:]
      dot2 = strstrt(modelext, '.')
      model = modelext[0:dot2-1]
      titles[i] = 'RCMC'.'-'.model
   }
   files[i] = file
}

# set graph's style
set style fill transparent solid 0.5 noborder
set boxwidth 0.9
set key left top
set xtics 1
set logscale y
set decimal locale
set format y "%'g"

# set lines' style
set style line 1 fc rgb "green"   ps 1
set style line 2 fc rgb "purple"  ps 1
set style line 3 fc rgb "#DC143C" ps 1 pt 9
set style line 4 fc rgb "#6A5ACD" ps 1

# plot the graph
plot for [i=1:toolnum] files[i] using 2 title titles[i] with lines ls i
